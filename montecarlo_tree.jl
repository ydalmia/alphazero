import Chess.Move

# conceptualize a montecarlo tree as having nodes corresponding
# to different states, and edges representing transitions between states:
# then the below tree structure stores transitions as nodes, flipping the
# common mental model. this is because alphazero is concerned with storing 
# P, W, N statistics for each transition that it explores. It is is 
# unnecessary to store state of the game, since this can be generated by 
# traversing down the tree.
mutable struct TreeNode
    isleaf::Bool # at the bottom of the mcts search tree?
    W::Float32 # store the children's Weight
    N::Float32 # ... Count

    A::Union{Move, Nothing}    # UCI move to get here
    P::Float32 # ... Policy
    
    parent::Union{TreeNode, Nothing} # store pointer to parent
    children::Union{Vector{TreeNode}, Nothing} # store pointer to each child node
    
    TreeNode() = new(
        true,
        0.0,
        0.0,
        nothing, 
        0.0, 
        nothing, 
        nothing
    )
end

function TreeNode(a::Move, p::Float32, parent::TreeNode)
    node = TreeNode() 
    node.A = a
    node.P = p
    node.parent = parent
    return node
end

function children_stats(node::TreeNode)
    n = length(node.children)
    W = Array{Float32}(undef, n)
    N = Array{Float32}(undef, n)
    P = Array{Float32}(undef, n)
    A = []

    # see https://docs.julialang.org/en/v1/base/multi-threading/#Base.Threads.atomic_cas!
    for (i, child) in enumerate(node.children)
        W[i] = child.W # returns child.W without getting corrupted
        N[i] = child.N # ... N
        P[i] = child.P
        push!(A, child.A)
    end
    return W, N, P
end

function print_tree(node::TreeNode, depth=0; maxdepth=Inf)
    if depth < maxdepth
        print_node(node, depth)
        if !(node.children === nothing)
            for child in node.children
                print_tree(child, depth+1, maxdepth=maxdepth)
            end
        end
    end
end

function print_node(node::TreeNode, depth)
    if node.N == 0.0
        return
    end
    for _ in 0:depth
        print("  ")
    end
    println("A: ", node.A, "  N: ", node.N, "    W: ", node.W)
end